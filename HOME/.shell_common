# ==============================================================================
# Common Shell Configuration
# ==============================================================================
# bash/zsh共通設定。各シェルのrcファイルからsourceする。

# ------------------------------------------------------------------------------
# Environment Variables
# ------------------------------------------------------------------------------

export EDITOR="nvim"
export LANG="en_US.UTF-8"

# Claude Code: disable terminal title override (use shell's title instead)
export CLAUDE_CODE_DISABLE_TERMINAL_TITLE=1

# ------------------------------------------------------------------------------
# Path
# ------------------------------------------------------------------------------

# Homebrew
if [[ -d "/opt/homebrew/bin" ]]; then
    export PATH="/opt/homebrew/bin:$PATH"
fi

# Local bin
if [[ -d "$HOME/.local/bin" ]]; then
    export PATH="$HOME/.local/bin:$PATH"
fi

# ------------------------------------------------------------------------------
# Multiplexer Auto-start (tmux preferred, zellij fallback)
# ------------------------------------------------------------------------------

if [[ -z "$TMUX" && -z "$ZELLIJ" && $- == *i* ]]; then
    if command -v tmux &>/dev/null; then
        exec tmux new-session
    elif command -v zellij &>/dev/null; then
        exec zellij
    fi
fi

# ------------------------------------------------------------------------------
# Aliases
# ------------------------------------------------------------------------------

# ls
alias ll='ls -la'
alias la='ls -A'
alias l='ls -CF'

# Safety
alias rm='rm -i'
alias cp='cp -i'
alias mv='mv -i'

# Navigation
alias ..='cd ..'
alias ...='cd ../..'

# Editor (Neovim)
alias vi='nvim'
alias vim='nvim'

# Git
alias gs='git status'
alias gl='git log --oneline -10'
# git wrapper: サブコマンド拡張をシェル関数で処理
git() {
    case "$1" in
        wt)
            shift
            wtp "$@"
            ;;
        cw)
            shift
            if [[ "$1" == "-b" ]]; then
                [[ -z "$2" ]] && { echo "usage: git cw -b <new-branch>" >&2; return 1; }
                wtp add -b "$2" && wtp cd "$2"
                return
            fi
            if [[ -n "$1" ]]; then
                local target
                target=$(command git worktree list | awk -v pat="$1" '$0 ~ pat {print $1; exit}')
                if [[ -n "$target" ]]; then
                    cd "$target"
                elif command git rev-parse --verify "$1" &>/dev/null; then
                    wtp add "$1" && wtp cd "$1"
                else
                    wtp add -b "$1" && wtp cd "$1"
                fi
                return
            fi
            local selection tab=$'\t'
            selection=$(
                {
                    command git worktree list | while IFS= read -r line; do
                        hash=$(echo "$line" | awk '{print $2}')
                        subject=$(command git log -1 --format='%s' "$hash" 2>/dev/null)
                        printf 'W\t%s  %s\n' "$line" "$subject"
                    done
                    command git branch --sort=-committerdate | sed 's/^[* ]*//' | while IFS= read -r b; do
                        printf 'L\t%s\n' "$b"
                    done
                    command git branch -r --sort=-committerdate | grep -v HEAD | sed 's/^ *//' | while IFS= read -r b; do
                        printf 'R\t%s\n' "$b"
                    done
                } | fzf +m --delimiter '\t' --with-nth 2.. --header 'Select worktree / branch'
            ) || return
            [[ -z "$selection" ]] && return
            local type="${selection%%$tab*}"
            local value="${selection#*$tab}"
            case "$type" in
                W)
                    local dir
                    dir=$(echo "$value" | awk '{print $1}')
                    [[ -n "$dir" ]] && cd "$dir"
                    ;;
                L)
                    wtp add "$value" && wtp cd "$value"
                    ;;
                R)
                    local branch
                    branch=$(echo "$value" | sed 's#^[^/]*/##')
                    wtp add "$branch" && wtp cd "$branch"
                    ;;
            esac
            ;;
        cb)
            shift
            if [[ "$1" == "-b" ]]; then
                [[ -z "$2" ]] && { echo "usage: git cb -b <new-branch>" >&2; return 1; }
                command git checkout -b "$2"
                return
            fi
            if [[ -n "$1" ]]; then
                if command git rev-parse --verify "$1" &>/dev/null; then
                    command git checkout "$1"
                elif command git rev-parse --verify "origin/$1" &>/dev/null; then
                    command git checkout "$1"
                else
                    command git checkout -b "$1"
                fi
                return
            fi
            local branch
            branch=$(
                command git branch --all --sort=-committerdate |
                grep -v HEAD |
                sed 's/^[* ]*//' |
                awk '{ key=$0; sub("^remotes/[^/]+/", "", key) } !seen[key]++' |
                fzf +m
            ) && [[ -n "$branch" ]] && command git checkout "${branch#remotes/*/}"
            ;;
        repo)
            local dir
            dir=$(ghq list -p | fzf +m) && [[ -n "$dir" ]] && cd "$dir"
            ;;
        *)
            command git "$@"
            ;;
    esac
}

# Claude Code
alias fclaude='claude --dangerously-skip-permissions'

# cat (bat with syntax highlight)
cat() {
    if [[ "$1" == -* ]]; then
        bat "$@"
    else
        bat -pp "$@"
    fi
}

# Markdown viewer
alias md='glow -p'

# ------------------------------------------------------------------------------
# fzf
# ------------------------------------------------------------------------------

# Default options
export FZF_DEFAULT_OPTS='--height 40% --reverse --border
  --bind "ctrl-k:kill-line"
  --bind "ctrl-n:down,ctrl-p:up"
  --bind "alt-j:down,alt-k:up"
  --bind "ctrl-v:page-down,alt-v:page-up"
  --bind "alt-n:preview-down,alt-p:preview-up"'

# Use fd if available (faster than find)
if command -v fd &>/dev/null; then
    export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND='fd --type d --hidden --follow --exclude .git'
fi

# Preview with bat (syntax highlight) or head fallback
if command -v bat &>/dev/null; then
    export FZF_CTRL_T_OPTS="--preview 'bat --color=always --line-range :100 --style=numbers {}'"
elif command -v batcat &>/dev/null; then
    export FZF_CTRL_T_OPTS="--preview 'batcat --color=always --line-range :100 --style=numbers {}'"
else
    export FZF_CTRL_T_OPTS="--preview 'head -100 {}'"
fi

# fzf git branch checkout
fbr() {
    local branch
    branch=$(git branch --all | grep -v HEAD | fzf +m | sed 's/.* //' | sed 's#remotes/origin/##') && git checkout "$branch"
}

# fzf git log
flog() {
    git log --oneline --color=always | fzf --ansi --preview 'git show --color=always {1}' | awk '{print $1}'
}

# fzf file search & bat preview
fcat() {
    local file
    file=$(fzf --preview 'bat --color=always --style=numbers --line-range :200 {}') && bat "$file"
}

# ------------------------------------------------------------------------------
# Functions
# ------------------------------------------------------------------------------

# mkdir & cd
mkcd() {
    mkdir -p "$1" && cd "$1"
}

# ------------------------------------------------------------------------------
# Pane Title (tmux / terminal)
# ------------------------------------------------------------------------------

# Get directory name for title (git root name or current dir name)
_pane_title_dir() {
    local git_root
    git_root=$(git rev-parse --show-toplevel 2>/dev/null)
    if [[ -n "$git_root" ]]; then
        basename "$git_root"
    else
        basename "$PWD"
    fi
}

# Set pane title
_pane_set_title() {
    local title="$1"
    printf '\e]0;%s\a' "$title"
}
